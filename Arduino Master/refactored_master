#include "Wire_Jens.h"
#include "WiFiEsp.h"
#include <SPI.h>
#include <SD.h>
#include "df_can.h"

#define SD_CS 4

const int SPI_CS_PIN = 10;
MCPCAN CAN(SPI_CS_PIN); 

union sensor_data_t
{
  float value;
  byte bytes[4];
};

float sensor1_value;
float sensor2_value;
float sensor3_value;

uint32_t time_start;
uint32_t time_now;

bool can_update;
bool joystick_update;
bool sync;

unsigned char flagRecv = 0;
unsigned char len = 0;
unsigned char buf[8];
char str[20];

/*
   Set network SSID (network name)
   Set network password
   Set wifi radio status
   reqCount saves the number of requests made
*/
char ssid[] = "ARDUINO_SERVER";
char pass[] = "123456789";
int status = WL_IDLE_STATUS;
int reqCount = 0;   /
int record;
String data;

WiFiEspServer server(8080);

void setup() {

  /*  Initialize Serial for monitor
      Initialize Serial1 for ESP module (Wifi)
      Initialize the I2C communication channel
  */
  Serial.begin(9600);
  Serial1.begin(115200);
  WiFi.init(&Serial1);
  Wire.begin();

  /*
      Count = The max numbers of initializations for the CAN-BUS, if initialize failed first!
      can_update = boolean set of a new CAN message has been received
      joystick_update = boolean set when no CAN message has been received for some time
      sync = boolean for starting the sync of data on SD card with the phone
  */
  int count = 50;
  can_update = false;
  joystick_update = false;
  sync = false;

  /*
     Initialize the CAN interface and shield
     Set CAN bus baudrate at 500kbps
  */
  do {
    CAN.init();
    if (CAN_OK == CAN.begin(CAN_500KBPS))
    {
      Serial.println("DFROBOT's CAN BUS Shield init ok!");
      break;
    }
    else
    {
      Serial.println("DFROBOT's CAN BUS Shield init fail");
      Serial.println("Please Init CAN BUS Shield again");

      delay(100);
      if (count <= 1)
        Serial.println("Please give up trying!, trying is useless!");
    }

  } while (count--);

  /* Set CAN bus Interrupt
  */
  attachInterrupt(0, MCP2515_ISR, FALLING);


  /* WIFI MODULE
     Initialize ESP module, configure access point
     Start the TCP server on port 8080
  */
  if (WiFi.status() == WL_NO_SHIELD) {
    Serial.println(F("WiFi shield not present"));
    while (true);
  }
  Serial.print(F("Attempting to start AP "));
  Serial.println(ssid);

  IPAddress localIp(192, 168, 1, 1);
  WiFi.configAP(localIp);
  status = WiFi.beginAP(ssid, 10, pass, ENC_TYPE_WPA2_PSK);

  Serial.println(F("Access point started"));
  printWifiStatus();

  server.begin();
  Serial.println(F("TCP Server started"));
  delay(3000);
  time_start = millis();


  /* SD CARD
     See if the card is present and can be initialized
     If SD Card fails, program will do nothing: infinite while-loop
  */
  pinMode(SD_CS, OUTPUT);
  digitalWrite(SD_CS, 1);
  Serial.print("Initializing SD card...");
  if (!SD.begin(SD_CS)) {
    Serial.println("Card failed, or not present");
    while (1);
  }
  Serial.println("card initialized.");

}

void loop() {

  /*
     Listen for incoming client connections
     When there is a client connected: ONLINE SCENARIO
     1. Sync any data from the SD card to the phone
     2. Update the joystick_update boolean: When 2 seconds have passed, joystick_update is set to true.
     3. Check if any CAN bus data had been written in the CAN buffer: Check this with can_update
     4. If 3. was true -> ask for GPS and Joystick values from slave, concatenate with new CAN data and send to driver Phone
     5. If 3. was false -> ask for GPS and Joystick values from slave and send to driver Phone
  */
  WiFiEspClient client = server.available();
  
  while (client) {
    SD_sync(client);

    String data;
    time_now = millis();
    if (time_now - time_start > 2000) {
      joystick_update = true;
      time_start = millis();
    } else {
      joystick_update = false;
    }

    if (can_update) {
      Wire.requestFrom(8, 96);
      char c;
      while (Wire.available())
      {
        c = Wire.read();
        if (strcmp(c, '#') == 0) break;
        data += c;
      }
      data += " S1: " + String(sensor1_value) + " S2: " + String(sensor2_value) + " S3: " + String(sensor3_value);
      if (client.connected()) {
        sendResponse(client, data);
        delay(10);
      } else {
        client.stop();
        Serial.println(F("Client disconnected"));
      }
      can_update = false;
    } else if (joystick_update) {
      Wire.requestFrom(8, 96);
      char c;
      while (Wire.available())
      {
        c = Wire.read();
        if (strcmp(c, '#') == 0) break;
        data += c;
      }
      joystick_update = false;

      if (client.connected()) {
        sendResponse(client, data);
        delay(10);
      } else {
        client.stop();
        Serial.println(F("Client disconnected"));
      }
    }
    CanGetData();
  }

  /*
    When there is no client connected: OFFLINE SCENARIO
    1. Update the joystick_update boolean: When 2 seconds have passed, joystick_update is set to true.
    2. Check if any CAN bus data had been written in the CAN buffer: Check this with can_update
    3. If 2. was true -> ask for GPS and Joystick values from slave, concatenate with new CAN data and log on SD card
    4. If 2. was false -> ask for GPS and Joystick values from slave and log on SD card
  */
  String data;
  time_now = millis();
  if (time_now - time_start > 2000) {
    joystick_update = true;
    time_start = millis();
  } else {
    joystick_update = false;
  }

  if (can_update) {
    Wire.requestFrom(8, 96);
    char c;
    while (Wire.available())
    {
      c = Wire.read();
      if (strcmp(c, '#') == 0) break;
      data += c;
    }
    data += " S1: " + String(sensor1_value) + " S2: " + String(sensor2_value) + " S3: " + String(sensor3_value);
    SD_log(data);
    can_update = false;
  } else if (joystick_update) {
    Wire.requestFrom(8, 96);
    char c;
    while (Wire.available())
    {
      c = Wire.read();
      if (strcmp(c, '#') == 0) break;
      data += c;
    }
    joystick_update = false;
    SD_log(data);
  }

  CanGetData();
  sync = true;
}

void MCP2515_ISR()
{
  flagRecv = 1;
}

/*
   flagrecv is set in the intterupt routine of the CAN bus buffer
   CAN data is parsed and stored into global sensor values
   can_update is set true to send data to phone or log data on SD card in next cycle
*/
void CanGetData()
{
  if (flagRecv)
  {
    int id;
    while (CAN_MSGAVAIL == CAN.checkReceive())
    {
      CAN.readMsgBuf(&len, buf);
      id = CAN.getCanId();
      sensor_data_t sensor_data;
      for (int i = 0; i < len; i++)
      {
        sensor_data.bytes[i] = buf[i];
      }
      Serial.println(sensor_data.value);
      switch (id) {
        case 1:
          sensor1_value = sensor_data.value;
          break;
        case 2:
          sensor2_value = sensor_data.value;
          break;
        case 3:
          sensor3_value = sensor_data.value;
          break;
      }
      flagRecv = 0;
      can_update = true;
    }
  }
}

void sendResponse(WiFiEspClient client, String data)
{
  client.println(data);
}


void printWifiStatus()
{
  IPAddress ip = WiFi.localIP();
  Serial.println(ip);
}


void SD_log(String data)
{
  File dataFile = SD.open("data.txt", FILE_WRITE);
  if (dataFile) {
    Serial.println(data);
    dataFile.println(data);
    dataFile.close();
  }
  else {
    Serial.println("error opening data.txt");
  }
}


bool SD_sync(WiFiEspClient client)
{
  String buff;
  bool sync = true;

  File dataFile = SD.open("data.txt", FILE_READ);
  while (sync) {
    if (dataFile.available() > 0) {
      buff = dataFile.readStringUntil('\n');
      Serial.println(buff); // Debugging
    } else {
      dataFile.close();
      SD.remove("data.txt");
      sync = false;
      break;
    }

    if (client.connected()) {
      sendResponse(client, buff);
      delay(10);
    } else {
      client.stop();
      sync = false;
    }
  }
  dataFile.close();
  return sync;
}
